apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: "jacoco"
apply plugin: 'com.github.kt3k.coveralls'

sourceCompatibility = 1.7
targetCompatibility = 1.7
compileJava.options.encoding = "UTF-8"
version = '0.3.1'
mainClassName = 'focusedCrawler.Main'
applicationDefaultJvmArgs = ["-Dname=ache -XX:+UseConcMarkSweepGC -XX:+HeapDumpOnOutOfMemoryError"]

repositories {
    mavenCentral()
    maven { url "http://download.oracle.com/maven/" } // for BerkeleyDB
}

dependencies {
    
    compile 'org.slf4j:slf4j-api:1.7.+'
    compile 'ch.qos.logback:logback-classic:1.1.+'
    compile 'commons-cli:commons-cli:1.3'
    compile 'com.google.guava:guava:18.0'
    compile 'net.sourceforge.nekohtml:nekohtml:1.9.22'
    compile 'org.eclipse.birt.runtime.3_7_1:org.w3c.css.sac:1.3.0'
    compile 'nz.ac.waikato.cms.weka:weka-stable:3.6.13'
    compile 'org.apache.lucene:lucene-core:4.10.4'
    compile 'org.elasticsearch:elasticsearch:1.4.4'
    compile 'commons-codec:commons-codec:1.9'
    compile 'commons-validator:commons-validator:1.4.1'
    compile 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.4' 
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.5.4'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.5.4'
    compile 'com.syncthemall:boilerpipe:1.2.2'
    compile 'com.sleepycat:je:3.3.75' // BerkeleyDB
    compile 'net.sourceforge.htmlunit:htmlunit:2.8'
    compile 'org.apache.commons:commons-compress:1.9'
    compile 'org.apache.httpcomponents:httpclient:4.4.1'
    compile 'org.apache.tika:tika-parsers:1.9'
    
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    
    // TODO fill these in from Maven Central instead of that local libs dir
    // TODO recommend replacing these with:
    // compile 'com.cybozu.labs:langdetect:1.1-20120112'
    compile files('libs/jsonic-1.2.0.jar') // recommend updating this, it's for langdetect.jar
    compile files('libs/langdetect-03-03-2014.jar') // TODO why isn't this in maven central

}

// TODO create binary for running focusedCrawler.link.LinkStorage, focusedCrawler.crawler.CrawlerManager, focusedCrawler.link.frontier.AddSeeds


//
// Adds version to final JAR artifact
//
jar {
  manifest {
    attributes(
      "Implementation-Title": project.name,
      "Implementation-Version": version
    )
  }
}


//
// Copies config folder into final distribution file
//
task copyConfig {
    def f = file("$buildDir/config")
    outputs.dir f
    doLast {
        copy {
            from "config/sample_config"
            into "$buildDir/config"
        }
    }
}
applicationDistribution.from(copyConfig) {
    into "config"
}


//
// Integration for test coverage service: coveralls
//
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}
jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}