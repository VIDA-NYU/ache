apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: "jacoco"
apply plugin: 'com.github.kt3k.coveralls'

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"
version = '0.9.0-SNAPSHOT'
mainClassName = 'focusedCrawler.Main'
applicationDefaultJvmArgs = ["-Dname=ache -XX:+UseConcMarkSweepGC -XX:+HeapDumpOnOutOfMemoryError"]

repositories {
    mavenCentral()
    maven { url "http://download.oracle.com/maven/" } // for BerkeleyDB
}

dependencies {
    // Logging
    compile 'org.slf4j:slf4j-api:1.7.+'
    compile 'ch.qos.logback:logback-classic:1.1.+'
    // Commons
    compile 'com.google.guava:guava:20.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-compress:1.12'
    compile 'commons-codec:commons-codec:1.10'
    compile 'commons-validator:commons-validator:1.5.1'
    compile 'com.github.crawler-commons:crawler-commons:0.7'
    compile 'org.apache.httpcomponents:httpclient:4.5.2'
    // CLI
    compile 'io.airlift:airline:0.7'
    // Data serialization
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.8.5'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.8.5'
    compile 'com.esotericsoftware:kryo:4.0.0'
    // Database engines
    compile 'org.rocksdb:rocksdbjni:4.11.2'
    compile 'com.sleepycat:je:3.3.75' // BerkeleyDB
    // Others
    compile 'org.apache.tika:tika-parsers:1.14'
    compile 'com.syncthemall:boilerpipe:1.2.2'
    compile 'net.sourceforge.nekohtml:nekohtml:1.9.22'
    compile 'nz.ac.waikato.cms.weka:weka-stable:3.6.13'
    compile 'org.apache.lucene:lucene-core:4.10.4'
    compile 'org.elasticsearch:elasticsearch:1.4.4'
    compile 'org.elasticsearch.client:rest:5.3.0'
    compile 'io.dropwizard.metrics:metrics-core:3.1.3'
    compile 'io.dropwizard.metrics:metrics-json:3.1.3'
    compile 'io.dropwizard.metrics:metrics-jvm:3.1.3'
    compile 'com.amazonaws:aws-java-sdk:1.11.150'
    compile 'org.jsoup:jsoup:1.10.3'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'

    compile('org.netpreserve.commons:webarchive-commons:1.1.8') {
      exclude group: 'org.apache.hadoop', module: 'hadoop-core'
      exclude group: 'junit', module: 'junit'
    }

    // REST server dependencies
    compile "com.sparkjava:spark-core:2.5.3"
    
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.eclipse.jetty:jetty-server:9.3.6.v20151106' // for tests of crawler commons library fork
    testCompile 'com.squareup.okhttp3:mockwebserver:3.8.1'
    
    // TODO fill these in from Maven Central instead of that local libs dir
    compile files('libs/jsonic-1.2.0.jar') // recommend updating this, it's for langdetect.jar
    compile files('libs/langdetect-03-03-2014.jar') // TODO why isn't this in maven central
}


//
// Make sure that ache-dashboard is compiled and copied into resources folder
// before the resources are processed and bundled into the JAR file
//
processResources {
    dependsOn ':ache-dashboard:install'
}


//
// Adds version to final JAR artifact
//
jar {
  manifest {
    attributes(
      "Implementation-Title": project.name,
      "Implementation-Version": version
    )
  }
}


//
// Copies config folder into final distribution file
//
task copyConfig {
    def f = file("$buildDir/config")
    outputs.dir f
    doLast {
        copy {
            from "config/"
            into "$buildDir/config"
            exclude "sample_model", "sample_training_data", "sample.seeds"
        }
    }
}
applicationDistribution.from(copyConfig) {
    into "config"
}


//
// Integration for test coverage service: coveralls
//
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}
jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(
                dir: it,
                exclude: ['focusedCrawler/tools/**']
            )
        })
    }
}


